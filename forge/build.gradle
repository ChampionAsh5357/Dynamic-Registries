// Add plugins for ForgeGradle
plugins {
    id 'library-conventions'
    id 'license-manager'
    id 'net.minecraftforge.gradle' version '5.1.+'
    //id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' TODO: Add back in later
}

// Set project information
classifier.id = ''
classifier.version = "$project.forgeMinecraftVersion-$project.projectVersion"

// Include resources generated by data generators
sourceSets {
    generated {}
    main.resources {
        srcDir 'src/generated/resources'
    }
    testmod {}
}

minecraft {
    // The mappings can be changed at any time, and must be in the following format:
    // Channel:   Version:
    // snapshot   YYYYMMDD   Snapshot are built nightly
    // stable     #          Stables are built at the discretion of the MCP team
    // official   MCVersion  Official field/method names from Mojang mapping files
    //
    // You must be aware of the Mojang license when using the 'official' mappings
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Use non-defaultW mappings at your own risk. they may not always work
    // Simply re-run your setup task after changing the mappings to update your workspace
    mappings channel: project.forgeMappingChannel, version: project.forgeMappingVerison
    
    // Default run configurations
    // These can be tweaked, removed, or duplicated as needed
    runs {
        // Client run configuration
        client {
            // Directory for the project to run in
            workingDirectory file('run/client')

            // Add user credentials and mixin information
            args '--accessToken', project.minecraftAccessToken//,
                //'-mixin.config=' + project.projectBaseName + '.mixins.json'

            // Add IntelliJ module naming
            ideaModule "${project.projectBaseName}.forge.main"

            // Rename task to be unique
            taskName 'runForgeClient'

            // Set the console logging level
            property 'forge.logging.console.level', 'debug'

            // Attach the sources to the run
            mods.create(project.projectBaseName).source(sourceSets.main)
            mods.create(project.projectBaseName + '_test').source(sourceSets.testmod)
        }

        // Server run configuration
        server {
            workingDirectory file('run/server')
            //arg '-mixin.config=' + project.projectBaseName + '.mixins.json'
            ideaModule "${project.projectBaseName}.forge.main"
            taskName 'runForgeServer'
            property 'forge.logging.console.level', 'debug'
            mods.create(project.projectBaseName).source(sourceSets.main)
            mods.create(project.projectBaseName + '_test').source(sourceSets.testmod)
        }


        // Data run configuration
        data {
            workingDirectory file('run/data')
            ideaModule "${project.projectBaseName}.forge.main"
            taskName 'runForgeData'
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources
            args '--mod', project.projectBaseName,
                '--all',
                '--output', sourceSets.generated.resources.srcDirs[0],
                '--existing', sourceSets.main.resources.srcDirs[0]//,
                //'-mixin.config=' + project.projectBaseName + '.mixins.json'

            mods.create(project.projectBaseName).source(sourceSets.main)
            mods.create(project.projectBaseName + '_test').source(sourceSets.testmod)
        }
    }
}

configurations {
    testmodImplementation.extendsFrom implementation
}

// Include MinecraftForge as a dependency
dependencies {
    minecraft group: 'net.minecraftforge', name: 'forge', version: "$project.forgeMinecraftVersion-$project.forgeApiVersion"

    testmodImplementation sourceSets.main.output
}

jar {
    // Reobfuscate the jar file to be used in production
    finalizedBy 'reobfJar'

    // Add mixin to jar manifest
    /*manifest.attributes([
            'MixinConfigs': "${project.projectBaseName}.mixins.json"
    ])*/
}

// Setup mixin reference map
/*mixin {
    add sourceSets.main, "${project.projectBaseName}.refmap.json"
}*/

// Set specific publication data
publishing {
    publications.create(archivesBaseName, MavenPublication) {
        from components.java
        pom {
            name = "$project.projectName"
            description = "An implementation of dynamic registries based of the MinecraftForge registry system"
        }
    }
}
